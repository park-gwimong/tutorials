import org.hidetake.gradle.swagger.generator.GenerateSwaggerUI
import org.springframework.boot.gradle.tasks.bundling.BootJar

buildscript {
    ext {
        springBootVersion = '2.7.0'
        restdocsApiSpecVersion = '0.16.2'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}


plugins {
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'com.epages.restdocs-api-spec' version "${restdocsApiSpecVersion}"
    id 'io.spring.dependency-management' version '1.0.12.RELEASE'
    id 'java'
    id 'org.hidetake.swagger.generator' version '2.18.2'
}

group = 'dms-api'
version = 'v1.000-SNAPSHOT'
sourceCompatibility = 11


repositories {
    mavenCentral()
}

swaggerSources {
    sample {
        setInputFile(file("${project.buildDir}/api-spec/openapi3.yaml"))
    }
}

openapi3 {
    setServer("http://localhost:9001")
    title = "restdocs-swagger API Documentation"
    description = "Spring REST Docs with SwaggerUI."
    version = "0.0.1"
    format = "yaml"
}

configurations {
    providedRuntime
}

dependencies {

    /* Spring boot */
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-web-services'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'

    implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'

    swaggerUI 'org.webjars:swagger-ui:4.18.2'

    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    compileOnly 'org.springframework.boot:spring-boot-configuration-processor'

    /* Spring */
    implementation 'org.springframework.session:spring-session-core'

    /* db connector */
    runtimeOnly 'mysql:mysql-connector-java'

    /* lombok */
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "com.epages:restdocs-api-spec-restassured:${restdocsApiSpecVersion}"
    testImplementation 'io.rest-assured:rest-assured'

}


tasks.named('test') {
    useJUnitPlatform()
}

tasks.withType(GenerateSwaggerUI).configureEach {
    dependsOn 'openapi3'
}

tasks.register('copySwaggerUI', Copy) {
    dependsOn 'generateSwaggerUISample'

    def generateSwaggerUISampleTask = tasks.named('generateSwaggerUISample', GenerateSwaggerUI).get()

    from("${generateSwaggerUISampleTask.outputDir}")
    into("${project.buildDir}/resources/main/docs")
}

tasks.withType(BootJar).configureEach {
    dependsOn 'copySwaggerUI'
}